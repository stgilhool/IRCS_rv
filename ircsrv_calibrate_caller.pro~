function parscale_rescale, parinfo_str, llim=llim, ulim=ulim

;SCALE PARAMETERS TO BE BETWEEN 0 and 1, if limits are defined
if n_elements(ulim) eq 0 then ulim=1d0
if n_elements(llim) eq 0 then llim=0d0

for param=0, n_elements(parinfo_str)-1 do begin
    ;Set scaled parameter limits
    parinfo_str[param].limits=[llim, ulim]
    
    ;read in true parameter value and limits
    t_llim=parinfo_str[param].tlimits[0]
    t_ulim=parinfo_str[param].tlimits[1]
    t_val=parinfo_str[param].tvalue
    
    ;define scale factors
    pscale=1d0/(t_ulim-t_llim)
    pshift=llim-(pscale*t_llim)

    parinfo_str[param].parscale=[pscale,pshift]

    ;Scale the value and limits
    parinfo_str[param].limits=[llim, ulim]

    parinfo_str[param].value = (pscale * t_val) + pshift

endfor

return, parinfo_str

end


function param_scale_down, t_p, pscale, pshift

p = (pscale*t_p) + pshift

return, p

end


function param_scale_up, p, pscale, pshift

t_p = (p-pshift)/pscale

return, t_p

end


function parinfo_changeval, parinfo_str, vals, parname=parname, index=index

parnames=parinfo_str.parname

if n_elements(index) eq 0 then begin
    
    npar  = n_elements(parname)
    index = lonarr(npar)
    
    for param_ind = 0, npar - 1 do begin
        index[param_ind] = where(parnames eq parname[param_ind], icount)
    endfor
endif else npar = n_elements(index)


;Change values


parinfo_str[index].tvalue = vals

parinfo_str = parscale_rescale(parinfo_str)

return, parinfo_str

end


function parinfo_changelim, parinfo_str, lims, parname=parname, index=index, lower=lower, upper=upper, auto=auto

parnames=parinfo_str.parname

if n_elements(lower) ne 0 and n_elements(upper) ne 0 then begin
    lower = 0
    upper = 0
endif

if n_elements(lower) eq 0 then lower = 0
if n_elements(upper) eq 0 then upper = 0

if n_elements(index) eq 0 then begin
    
    npar  = n_elements(parname)
    index = lonarr(npar)
    
    for param_ind = 0, npar - 1 do begin
        index[param_ind] = where(parnames eq parname[param_ind], icount)
    endfor
endif else npar = n_elements(index)



;Change values
;check if using auto limits
if n_elements(auto) ne 0 then begin
    t_val = parinfo_str[index].tvalue
    t_llim = t_val * (1d0 - auto)
    t_ulim = t_val * (1d0 + auto)
    if lower eq 1 then lims = t_llim else $
      if upper eq 1 then lims = t_ulim else $
      lims = [t_llim, t_ulim]
endif 
    
if lower eq 1 then parinfo_str[index].tlimits[*,0] = lims else $
  if upper eq 1 then parinfo_str[index].tlimits[0,*] = lims else $
  parinfo_str[index].tlimits = lims

parinfo_str = parscale_rescale(parinfo_str)

return, parinfo_str

end


function parinfo_readin, model_id

path 		= ircsrv_paths()
model_path	= path.calib
parfile		= model_path+model_id

pinfo		= mrdfits(parfile, 1)

return, pinfo

end



function parinfo_all_init

nparam_max=48L

parstr={parname:'null', $
        value:0d0, $
        step:0d0, $
        relstep:0d0, $
        fixed:1, $
        limited:[0,0], $
        limits:[0d0, 0d0], $
        mpside:0, $
        mpmaxstep:0, $
        mpprint:1, $
        bestfit:!values.d_nan, $
        tbestfit:!values.d_nan, $
        tvalue:0d0, $
        tlimits:[0d0, 0d0], $
        parscale:[1d0,0d0], $
        used:0 $
         }


parinfo_all = replicate(parstr, nparam_max)

rvi=0
wli=lindgen(5)+1
d_wli=lindgen(3)+6
ghi=lindgen(11)+9
d_ghi=lindgen(11)+20
ti=lindgen(5)+31
ki=lindgen(12)+36


parnames=['d_rv', $
          'wl0','wl1','wl2','wl3','wl4', $
          'd_wl0','d_wl1','d_wl2', $
          'gh0','gh1','gh2','gh3','gh4','gh5','gh6','gh7','gh8','gh9','gh10', $
          'd_gh0','d_gh1','d_gh2','d_gh3','d_gh4','d_gh5','d_gh6','d_gh7','d_gh8','d_gh9','d_gh10', $
          't_nh3','t_h2o','t_co2ch4','t_co2','t_ch4', $
          'k0','k1','k2','k3','k4','k5','k6','k7','k8','k9','k10','k11' $
          ]

parinfo_all.parname=parnames

;VALUE
values=dblarr(nparam_max)

values[rvi]   	=	[0d0]
values[wli]	=	[2.29107d0, 6.255d-5, -2.046d-9, -1.55d-12, 0d0]
values[d_wli]	=	[0d0, 0d0, 0d0]
values[ghi]	=	[0.7d0,0.05d0,0d0,0d0,0d0,0d0,0d0,0d0,0d0,0d0,0d0]
values[d_ghi]	=	[0d0,0d0,0d0,0d0,0d0,0d0,0d0,0d0,0d0,0d0,0d0]
values[ti]	=	[0.95d0,0.3d0,0.3d0,0d0,0d0]
values[ki]	=	[1d0,1d0,1d0,1d0,1d0,1d0,1d0,1d0,1d0,1d0,1d0,1d0]

parinfo_all.tvalue=values

;STEP
steps=dblarr(nparam_max)          

;steps[rvi]	=	[1d-1]
;steps[wli]	=	[1d-1, 1d-6, 0d0, 0d0, 0d0]
;steps[d_wli]	=	[1d-6, 0d0, 0d0]
;steps[ghi]	=	[0.1d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0]
;steps[d_ghi]	=	[0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0, 0d0]
;steps[ti]       =	[1d-2, 1d-1, 1d-1, 0d0, 0d0]
;steps[ki]	=	[1d-1,1d-1,1d-1,1d-1,1d-1,1d-1,1d-1,1d-1,1d-1,1d-1,1d-1,1d-1]

;parinfo_all.step=steps

;LIMITED
parinfo_all[rvi].limited	=	[0,0]
parinfo_all[wli].limited	=	[[1,1],[1,1],[0,0],[0,0],[0,0]]
parinfo_all[d_wli].limited	=	[[1,1],[0,0],[0,0]]
parinfo_all[ghi].limited	=	[[1,1],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0]]
parinfo_all[d_ghi].limited	=	[[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0],[0,0]]
parinfo_all[ti].limited		=	[[1,1],[1,1],[1,1],[0,0],[0,0]]
parinfo_all[ki].limited		=	[[1,1],[1,1],[1,1],[1,1],[1,1],[1,1],[1,1],[1,1],[1,1],[1,1],[1,1],[1,1]]

;LIMITS
parinfo_all[rvi].tlimits	=	[-2d0,2d0]
parinfo_all[wli].tlimits	=	[[2.27d0,2.31d0],[4d-5,7d-5],[-1d-8,1d-8],[-1d-11,1d-11],[-1d-15,1d-15]]
parinfo_all[d_wli].tlimits	=	[[-1d5,1d-5],[-1d-6,1d-6],[-1d-7,1d-7]]
parinfo_all[ghi].tlimits	=	[[0.3d0,1.5d0],[-0.2d0,0.2d0],[-0.2d0,0.2d0],[-0.2d0,0.2d0],[-0.2d0,0.2d0],$
                                         [-0.2d0,0.2d0],[-0.2d0,0.2d0],[-0.2d0,0.2d0],[-0.2d0,0.2d0],[-0.2d0,0.2d0],[-0.2d0,0.2d0]]
parinfo_all[d_ghi].tlimits	=	[[-2d-1,2d-1],[-2d-1,2d-1],[-2d-1,2d-1],[-2d-1,2d-1],[-2d-1,2d-1],[-2d-1,2d-1],[-2d-1,2d-1],$
                                        [-2d-1,2d-1],[-2d-1,2d-1],[-2d-1,2d-1],[-2d-1,2d-1]]
parinfo_all[ti].tlimits		=	[[0d0,1d0],[0d0,1d0],[0d0,1d0],[0d0,1d0],[0d0,1d0]]
parinfo_all[ki].tlimits		=	[[0.8d0,1.2d0],[0.8d0,1.2d0],[0.8d0,1.2d0],[0.8d0,1.2d0],$
                                        [0.9d0,1.1d0],[0.9d0,1.1d0],[0.9d0,1.1d0],[0.9d0,1.1d0], $
					[0.9d0,1.1d0],[0.9d0,1.1d0],[0.9d0,1.1d0],[0.9d0,1.1d0]]
;MPSIDE
parinfo_all[rvi].mpside		=	2
parinfo_all[wli].mpside		=	[0, 0, 0, 0, 0]
parinfo_all[d_wli].mpside	=	[2, 0, 0]
parinfo_all[ghi].mpside		=	[2,0,0,0,0,0,0,0,0,0,0]



;parinfo_all_copy = parinfo_all

parinfo_all = parscale_rescale(parinfo_all)



return, parinfo_all

end

pro parinfo_all_freepar, parinfo_all, n_rv=n_rv, n_wl=n_wl, n_d_wl=n_d_wl, n_gh=n_gh, n_d_gh=n_d_gh, n_t=n_t, n_k=n_k



if n_elements(n_rv) eq 0 then n_rv = 0
if n_elements(n_wl) eq 0 then n_wl = 0
if n_elements(n_d_wl) eq 0 then n_d_wl = 0
if n_elements(n_gh) eq 0 then n_gh = 0
if n_elements(n_d_gh) eq 0 then n_d_gh = 0
if n_elements(n_t) eq 0 then n_t = 0
if n_elements(n_k) eq 0 then n_k = 0

rvi=0
wli=lindgen(5)+1
d_wli=lindgen(3)+6
ghi=lindgen(11)+9
d_ghi=lindgen(11)+20
ti=lindgen(5)+31
ki=lindgen(12)+36

nfree = n_rv + n_wl + n_d_wl + n_gh + n_d_gh + n_t + n_k


;CREATE MASTER PARINFO STRUCTURE
;parinfo_all = parinfo_all_init()

;TURN ON FREE PARAMETERS
if n_rv gt 0 then begin
    if n_rv gt 1 then message,"Only 1 RV parameter allowed"
    parinfo_all[rvi].fixed = 0
    parinfo_all[rvi].used = 1
endif

if n_wl gt 0 then begin
    if n_wl gt 5 then message, "Only 5 WL parameters allowed"
    parinfo_all[wli[0:n_wl-1]].fixed 	= 0
    parinfo_all[wli[0:n_wl-1]].used 	= 1
endif

if n_d_wl gt 0 then begin
    if n_d_wl gt 3 then message, "Only 3 D_WL parameters allowed"
    parinfo_all[d_wli[0:n_d_wl-1]].fixed 	= 0
    parinfo_all[d_wli[0:n_d_wl-1]].used 	= 1
endif

if n_gh gt 0 then begin
    if n_gh gt 11 then message, "Only 11 GH parameters allowed"
    parinfo_all[ghi[0:n_gh-1]].fixed 	= 0
    parinfo_all[ghi[0:n_gh-1]].used 	= 1
endif

if n_d_gh gt 0 then begin
    if n_d_gh gt 11 then message, "Only 11 D_GH parameters allowed"
    parinfo_all[d_ghi[0:n_d_gh-1]].fixed 	= 0
    parinfo_all[d_ghi[0:n_d_gh-1]].used 	= 1
endif

if n_t gt 0 then begin
    if n_t gt 5 then message, "Only 5 TAU parameters allowed"
    parinfo_all[ti[0:n_t-1]].fixed 	= 0
    parinfo_all[ti[0:n_t-1]].used 	= 1
endif

if n_k gt 0 then begin
    if n_k gt 12 then message, "Only 12 K parameters allowed"
    parinfo_all[ki[0:n_k-1]].fixed 	= 0
    parinfo_all[ki[0:n_k-1]].used 	= 1
endif



end
    

pro ircsrv_calibrate_caller, parinfo_all, _extra=ex

!except=2

n_wl	= 5L
n_gh	= 5L
n_d_gh	= 0L
n_t	= 1L
n_k	= 6L

parinfo_all = parinfo_all_init()
parinfo_all_freepar, parinfo_all, n_wl=n_wl, n_gh=n_gh, n_d_gh=n_d_gh, n_t = n_t, n_k=n_k

parinfo_all = parinfo_changeval(parinfo_all, [2.2910724d0, $
                                              6.2542955d-05, $
                                              -1.9750797d-09, $  
                                              -1.7354107d-12, $
                                              3.8375217d-17, $
                                              0.744d0, $
                                              0.0645d0, $
                                              -6.4d-3, $
                                              -4.27d-4, $
                                              0.93504703d0, $      
                                              0.99837539d0, $
                                              0.99817149d0, $
                                              0.99914237d0, $
                                              0.99917083d0, $
                                              0.999d0, $
                                             0.999d0], $
                                parname=['wl0','wl1','wl2','wl3','wl4', $
                                         'gh0','gh1','gh2','gh3', $
                                         't_nh3','k0','k1', $
                                         'k2','k3','k4','k5'])



object='GJ273'
epoch='18Jan2011'
trace='AB1'

first_pix=0L
npix_select=512L
fmode='mpfit'
fit_measure='chi_vec'
visualize=1

ircsrv_calibrate_jul15, parinfo_all, object=object, epoch=epoch, trace=trace, first_pix=first_pix, npix_select=npix_select, fmode=fmode, fit_measure=fit_measure, visualize=visualize



end
